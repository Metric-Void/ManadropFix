plugins {
    id 'maven-publish'
    id 'java-gradle-plugin'
}

version = '0.5-SNAPSHOT'

dependencies {
    implementation gradleApi()
    shadow 'org.yaml:snakeyaml:1.29'
    shadow project(':util')
}

gradlePlugin {
    plugins {
        manadropfix {
            id = 'com.metricv.ManadropFix'
            implementationClass = 'fr.il_totore.manadrop.ManaDrop'
        }
    }
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/Metric-Void/ManadropFix")
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
        maven {
            // here your PROVIDE the PROJECT-URI for publishing your package
            // in the project-specific package-space which is also visible at
            // the group scope above
            url =  uri("https://gitlab.com/api/v4/projects/28955808/packages/maven")
            name = "Gitlab"
            credentials(HttpHeaderCredentials) {
                name = "Job-Token"
                value = System.getenv("CI_JOB_TOKEN")
            }
            authentication {
                header(HttpHeaderAuthentication)
            }
        }
    }
    publications {
        withType(MavenPublication) {
            artifact(shadowJar)
        }
    }
}

shadowJar {
    configurations = [project.configurations.shadow]
    classifier ''
}

jar {
    enabled false
    dependsOn shadowJar
}